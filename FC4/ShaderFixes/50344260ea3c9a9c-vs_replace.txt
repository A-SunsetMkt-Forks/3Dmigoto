// HUD - crosshair, map, grapple hud icon

cbuffer CSceneFireUiPrimitiveParameterProvider : register(b0)
{
  float4 _BackgroundColor : packoffset(c0);
  float4 _BordersColor : packoffset(c1);
  float4 _ColorAdd : packoffset(c2);
  float4 _ColorAdjustment : packoffset(c3);
  float4 _ColorMultiplier : packoffset(c4);
  float4 _ColorTransparent : packoffset(c5);
  float4x4 _Transform : packoffset(c6);
  float4x4 _UVTransform : packoffset(c10);
  float2 _ViewportScaleXY : packoffset(c14);
  float _BordersSizeFloatArray[8] : packoffset(c15);
  float _Desaturation : packoffset(c22.y);
  float _DistanceFieldFloatArray[18] : packoffset(c23);
  float _LinePercentage : packoffset(c40.y);
  float _TexHeight : packoffset(c40.z);
  float _TexRatio : packoffset(c40.w);
  float _TextWidth : packoffset(c41);
  float _Time : packoffset(c41.y);
}

// Copied from lighting shaders b0 with 3DMigoto
cbuffer CViewportShaderParameterProvider : register(b13)
{
  float4 _AmbientColorMultiplier : packoffset(c0);
  float4 _AmbientOcclusionRanges_SunOcclusionFactor : packoffset(c1);
  float4 _BloomAdaptationFactor : packoffset(c2);
  float4 _CameraDirection_FOVDeg : packoffset(c3);
  float4 _CameraDistances : packoffset(c4);
  float4 _CameraNearPlaneSize : packoffset(c5);
  float4 _CameraPositionFractions_ZOffsetWS : packoffset(c6);
  float4 _CameraPositionFractions_ZOffsetWS_Previous : packoffset(c7);
  float4 _CameraPosition_DistanceScale : packoffset(c8);
  float4 _CameraPosition_DistanceScale_Previous : packoffset(c9);
  float4x4 _CullingViewProjectionMatrix : packoffset(c10);
  float4 _DepthScale : packoffset(c14);
  float4 _DepthTextureRcpSize : packoffset(c15);
  float4 _DepthTextureScaleOffset : packoffset(c16);
  float4 _FogParams : packoffset(c17);
  float4 _FogSHB : packoffset(c18);
  float4 _FogSHG : packoffset(c19);
  float4 _FogSHR : packoffset(c20);
  float4 _HeightFogParams : packoffset(c21);
  float4x4 _InvProjectionMatrix : packoffset(c22);
  float4x4 _InvViewMatrix : packoffset(c26);
  float4x4 _ProjectionMatrix : packoffset(c30);
  float4 _TempSunDirection : packoffset(c34);
  float4x4 _ViewMatrix : packoffset(c35);
  float4 _ViewPoint_ShadowProjDepthMin : packoffset(c39);
  float4x4 _ViewProjectionMatrix : packoffset(c40);
  float4x4 _ViewRotProjectionMatrix : packoffset(c44);
  float4x4 _ViewRotProjectionMatrix_Previous : packoffset(c48);
  float4 _ViewportSize : packoffset(c52);
  float2 _MotionVectorOffsets : packoffset(c53);
  float3 _CameraRight : packoffset(c54);
  float3 _CameraUp : packoffset(c55);
  float3 _EnvironmentMapColorOffset : packoffset(c56);
  float3 _EnvironmentMapColorScale : packoffset(c57);
}

// XXX CAUTION XXX: Omitting the static breaks everything:
static const uint vb_stride = 20;

// Would be nice if I could use this struct somehow, but thanks to a) padding
// and b) fxc I can't find a way to do so and end up with a working shader:
// struct vb_entry
// {
//   float2 vb_position;
//   float2 vb_texcoord;
//   // Don't be fooled by the float4 in the input signature - in
//   // the vertex buffer this is just a single 4 byte value:
//   float color0;
// };

// Current vertex buffer copied into a constant buffer with 3DMigoto
cbuffer VertexBuffer : register(b11)
{
	// It would be lovely if I could just make an array of the above
	// struct, but thanks to HLSL padding rules I can't. Each struct in the
	// array would be padded to 32 bytes (multiple of 16 bytes).

	// Additionally, we have to use an array of four component floats here
	// to make sure HLSL doesn't try to insert any additional padding.

	// Instead, I'll manually calculate the offsets when I look this up.

	// For HLSL padding rules, see here:
	// https://msdn.microsoft.com/en-us/library/windows/desktop/bb509632(v=vs.85).aspx

	float4 vb_raw[4096];
}

float lookup_vb_offset(uint off)
{
	uint idx = off / 4;
	uint component = off % 4;

	// Is there a more direct way to do this?
	switch (component) {
		case 0:
			return vb_raw[idx].x;
		case 1:
			return vb_raw[idx].y;
		case 2:
			return vb_raw[idx].z;
		case 3:
			return vb_raw[idx].w;
	}
}


float2 lookup_vertex_position(uint id)
{
	uint off = id * vb_stride / 4;

	return float2(lookup_vb_offset(off), lookup_vb_offset(off+1));
}

float2 find_quad_center(uint id)
{
	id = id / 6 * 6;

	float2 pos1 = lookup_vertex_position(id);
	float2 pos2 = lookup_vertex_position(id + 2);

	return (pos1 + pos2) / 2;
}

Texture2D<float4> StereoParams : register(t125);
Texture1D<float4> IniParams : register(t120);

#include <ShaderFixes/crosshair.hlsl>

float4 calc_pos(float2 v0)
{
// Copy of the first hunk of code from the shader that calculates the output
// position
  float4 r0,r1,o0;
  r0.xy = v0.xy;
  r0.zw = float2(1.000000e+000,1.000000e+000);
  r1.x = dot(r0.xyww, _Transform._m00_m10_m20_m30);
  r1.y = dot(r0.xyww, _Transform._m01_m11_m21_m31);
  r1.xy = r1.xy * float2(2.000000e+000,2.000000e+000) + float2(-1.000000e+000,-1.000000e+000);
  o0.xy = float2(1.000000e+000,-1.000000e+000) * r1.xy;
  o0.z = dot(r0.xyww, _Transform._m02_m12_m22_m32);
  o0.w = dot(r0.xyzw, _Transform._m03_m13_m23_m33);
  return o0;
}

bool is_yellow_arrow(float2 v1) {
	// This icon is not as simple as a bounding box... think it's two
	// separate triangles?
	return ((v1.x == 0.561442554 || v1.x == 0.623961031 ||
		 v1.x == 0.561448753 || v1.x == 0.62396722) &&
		(v1.y == 0.361364365 || v1.y == 0.486409545 ||
		 v1.y == 0.361369759 || v1.y == 0.486414939));
}

bool is_minimap(float2 o0) {
	return (o0.x < -0.55 && o0.y < -0.3);
}

void main(
  float2 v0 : position0,
  float2 v1 : texcoord0,
  float4 v2 : color0,
  out float4 o0 : SV_Position0,
  out float4 o1 : TEXCOORD0,
  out float4 o2 : TEXCOORD1,

  uint id : SV_VertexID

  )
{

  float4 r0,r1;
  uint4 bitmask;
  r0.xy = v0.xy;
  r0.zw = float2(1.000000e+000,1.000000e+000);
  r1.x = dot(r0.xyww, _Transform._m00_m10_m20_m30);
  r1.y = dot(r0.xyww, _Transform._m01_m11_m21_m31);
  r1.xy = r1.xy * float2(2.000000e+000,2.000000e+000) + float2(-1.000000e+000,-1.000000e+000);
  o0.xy = float2(1.000000e+000,-1.000000e+000) * r1.xy;
  o0.z = dot(r0.xyww, _Transform._m02_m12_m22_m32);
  o0.w = dot(r0.xyzw, _Transform._m03_m13_m23_m33);
  o1.xyzw = v2.zyxw;
  o2.xy = v0.xy;
  o2.zw = v1.xy;

float4 stereo = StereoParams.Load(0);
float4 params = IniParams.Load(0);
float4 filter = IniParams.Load(int2(2,0));
float separation = stereo.x; float convergence = stereo.y;
float hud_depth = params.x; float tex_filter = filter.x; float partner_filter = filter.y;

if (params.w == 0) {
	// Depth buffer did not get copied. We are in a menu, so don't adjust the UI.
	return;
}

if (tex_filter == 2 && partner_filter == 1 && abs(o0.x) >= 0.999) {
	// Vignette around the map based on texture, pixel shader and X
	// coordinate at far left/right of screen. Want to expand this
	// horizontally so that it still moves to hud_depth, but fills up the
	// expanded stereo screen.  Move the left side of right eye left, and
	// the right side of the left eye right to stretch the effect.
	// SV_VertexID tells us which side we are on:
	if ((stereo.z ==  1 && (id == 0 || id == 1 || id == 5)) ||
	    (stereo.z == -1 && (id == 2 || id == 3 || id == 4))) {
		o0.x += separation * hud_depth * 2;
	}
} else if (tex_filter == 3 && (
		matches_corner(v1, 0.0185470581, 0.0341720581, 0.986338615, 0.994151115) /* Line crosshair (most weapons) */
	     || matches_corner(v1, 0.821269989, 0.852519989, 0.689431667, 0.751931667) /* Dot crosshair (e.g. repair tool) */
	     )) {
	// Matched crosshair texture AND texture coordinates
	o0.x += adjust_from_depth_buffer(0,0);
} else if (tex_filter == 3 && is_yellow_arrow(v1)) {
	// Some other floating HUD element. Use the vertex buffer to locate the
	// center of the icon and use that as the point to adjust it from.
	// TODO: Might be better to use the minimum adjustment out of all four
	// corners?
	float2 ipos = find_quad_center(id);
	float4 opos = calc_pos(ipos);
	o0.x += adjust_from_depth_buffer(opos.x, opos.y);
} else if (is_minimap(o0)) {
	// TODO: Depth test to make sure it isn't below scenery
	// Need to match all shaders and textures that create it
	o0.x += separation * hud_depth;
} else if (tex_filter == 5) {
	// Another floating HUD icon
	float2 ipos = find_quad_center(id);
	float4 opos = calc_pos(ipos);
	o0.x += adjust_from_depth_buffer(opos.x, opos.y);
} else if (tex_filter == 4) {
	o0.x += adjust_from_depth_buffer(0,0);
} else {
	o0.x += separation * hud_depth;
}

  return;
}

/*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
// Generated by Microsoft (R) HLSL Shader Compiler 9.29.952.3111
//
//
// Buffer Definitions: 
//
// cbuffer CSceneFireUiPrimitiveParameterProvider
// {
//
//   float4 _BackgroundColor;           // Offset:    0 Size:    16 [unused]
//   float4 _BordersColor;              // Offset:   16 Size:    16 [unused]
//   float4 _ColorAdd;                  // Offset:   32 Size:    16 [unused]
//   float4 _ColorAdjustment;           // Offset:   48 Size:    16 [unused]
//   float4 _ColorMultiplier;           // Offset:   64 Size:    16 [unused]
//   float4 _ColorTransparent;          // Offset:   80 Size:    16 [unused]
//   float4x4 _Transform;               // Offset:   96 Size:    64
//   float4x4 _UVTransform;             // Offset:  160 Size:    64 [unused]
//   float2 _ViewportScaleXY;           // Offset:  224 Size:     8 [unused]
//   float _BordersSizeFloatArray[8];   // Offset:  240 Size:   116 [unused]
//   float _Desaturation;               // Offset:  356 Size:     4 [unused]
//   float _DistanceFieldFloatArray[18];// Offset:  368 Size:   276 [unused]
//   float _LinePercentage;             // Offset:  644 Size:     4 [unused]
//   float _TexHeight;                  // Offset:  648 Size:     4 [unused]
//   float _TexRatio;                   // Offset:  652 Size:     4 [unused]
//   float _TextWidth;                  // Offset:  656 Size:     4 [unused]
//   float _Time;                       // Offset:  660 Size:     4 [unused]
//
// }
//
//
// Resource Bindings:
//
// Name                                 Type  Format         Dim Slot Elements
// ------------------------------ ---------- ------- ----------- ---- --------
// CSceneFireUiPrimitiveParameterProvider    cbuffer      NA          NA    0        1
//
//
//
// Input signature:
//
// Name                 Index   Mask Register SysValue  Format   Used
// -------------------- ----- ------ -------- -------- ------- ------
// position                 0   xy          0     NONE   float   xy  
// texcoord                 0   xy          1     NONE   float   xy  
// color                    0   xyzw        2     NONE   float   xyzw
//
//
// Output signature:
//
// Name                 Index   Mask Register SysValue  Format   Used
// -------------------- ----- ------ -------- -------- ------- ------
// SV_Position              0   xyzw        0      POS   float   xyzw
// TEXCOORD                 0   xyzw        1     NONE   float   xyzw
// TEXCOORD                 1   xyzw        2     NONE   float   xyzw
//
vs_4_0
dcl_constantbuffer cb0[10], immediateIndexed
dcl_input v0.xy
dcl_input v1.xy
dcl_input v2.xyzw
dcl_output_siv o0.xyzw, position
dcl_output o1.xyzw
dcl_output o2.xyzw
dcl_temps 2
mov r0.xy, v0.xyxx
mov r0.zw, l(0,0,1.000000,1.000000)
dp4 r1.x, r0.xyww, cb0[6].xyzw
dp4 r1.y, r0.xyww, cb0[7].xyzw
mad r1.xy, r1.xyxx, l(2.000000, 2.000000, 0.000000, 0.000000), l(-1.000000, -1.000000, 0.000000, 0.000000)
mul o0.xy, r1.xyxx, l(1.000000, -1.000000, 0.000000, 0.000000)
dp4 o0.z, r0.xyww, cb0[8].xyzw
dp4 o0.w, r0.xyzw, cb0[9].xyzw
mov o1.xyzw, v2.zyxw
mov o2.xy, v0.xyxx
mov o2.zw, v1.xxxy
ret 
// Approximately 12 instruction slots used
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*/
  
  
